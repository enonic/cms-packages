<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet exclude-result-prefixes="#all" version="2.0" xmlns="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:util="enonic:utilities" xmlns:portal="http://www.enonic.com/cms/xslt/portal" xmlns:xs="http://www.w3.org/2001/XMLSchema">

    <xsl:import href="image-functions.xsl"/>

    <!-- Returns scoped parameter from config as element()?  -->
    <xsl:function name="util:get-scoped-parameter" as="element()?">
        <xsl:param name="name" as="xs:string"/>
        <xsl:param name="path" as="xs:string"/>
        <xsl:param name="parameter" as="element()*"/>
        <xsl:call-template name="utilities.get-parameter">
            <xsl:with-param name="name" select="$name" tunnel="yes"/>
            <xsl:with-param name="path" select="$path"/>
            <xsl:with-param name="parameter" select="$parameter" tunnel="yes"/>
        </xsl:call-template>
    </xsl:function>

    <xsl:template name="utilities.get-parameter">
        <xsl:param name="name" tunnel="yes" as="xs:string"/>
        <xsl:param name="path" as="xs:string"/>
        <xsl:param name="parameter" tunnel="yes" as="element()*"/>
        <xsl:choose>
            <xsl:when test="$parameter[@name = $name and @path = $path]">
                <xsl:sequence select="$parameter[@name = $name and @path = $path][1]"/>
            </xsl:when>
            <xsl:when test="$path != ''">
                <xsl:call-template name="utilities.get-parameter">
                    <xsl:with-param name="path" select="substring-before($path, concat('/', tokenize($path, '/')[last()]))"/>
                </xsl:call-template>
            </xsl:when>
            <xsl:when test="$parameter[@name = $name and @path = '/']">
                <xsl:sequence select="$parameter[@name = $name and @path = '/'][1]"/>
            </xsl:when>
        </xsl:choose>
    </xsl:template>

    <!-- Displays image -->
    <xsl:template name="utilities.display-image">
        <xsl:param name="region-width" select="550" as="xs:integer"/>
        <xsl:param name="filter" as="xs:string?"/>
        <xsl:param name="imagesize" as="element()*"/>
        <xsl:param name="image" as="element()"/>
        <xsl:param name="size" as="xs:string?"/>
        <xsl:param name="background" as="xs:string?"/>
        <xsl:param name="format" as="xs:string?"/>
        <xsl:param name="quality" as="xs:string?"/>
        <xsl:param name="title" select="$image/title" as="xs:string?"/>
        <xsl:param name="alt" as="xs:string">
            <xsl:choose>
                <xsl:when test="$image/contentdata/description != ''">
                    <xsl:value-of select="$image/contentdata/description"/>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:value-of select="$image/title"/>
                </xsl:otherwise>
            </xsl:choose>
        </xsl:param>
        <xsl:param name="class" as="xs:string?"/>
        <xsl:param name="style" as="xs:string?"/>
        <xsl:param name="id" as="xs:string?"/>
        <xsl:variable name="width" select="util:image-size($region-width, $imagesize, $size, (), $filter, $image, ())"/>
        <xsl:variable name="height" select="util:image-size($region-width, $imagesize, $size, (), $filter, $image, 'height')"/>
        <img src="{portal:createImageUrl(util:image-attachment-key($image/@key, $region-width, $imagesize, $size, (), $filter, $image), util:image-filter($region-width, $imagesize, $size, (), $filter), $background, $format, $quality)}" alt="{$alt}">
            <xsl:if test="$title">
                <xsl:attribute name="title">
                    <xsl:value-of select="$title"/>
                </xsl:attribute>
            </xsl:if>
            <xsl:if test="$width">
                <xsl:attribute name="width">
                    <xsl:value-of select="$width"/>
                </xsl:attribute>
            </xsl:if>
            <xsl:if test="$height">
                <xsl:attribute name="height">
                    <xsl:value-of select="$height"/>
                </xsl:attribute>
            </xsl:if>
            <xsl:if test="$class != ''">
                <xsl:attribute name="class">
                    <xsl:value-of select="$class"/>
                </xsl:attribute>
            </xsl:if>
            <xsl:if test="$style != ''">
                <xsl:attribute name="style">
                    <xsl:value-of select="$style"/>
                </xsl:attribute>
            </xsl:if>
            <xsl:if test="$id != ''">
                <xsl:attribute name="id">
                    <xsl:value-of select="$id"/>
                </xsl:attribute>
            </xsl:if>
        </img>
    </xsl:template>
    
    <!-- Returns timezone as xs:string -->
    <xsl:function name="util:get-timezone" as="xs:string">
        <xsl:variable name="timezone">
            <xsl:choose>
                <xsl:when test="timezone-from-date(current-date()) lt xs:dayTimeDuration('PT0S')">-</xsl:when>
                <xsl:otherwise>+</xsl:otherwise>
            </xsl:choose>
            <xsl:value-of select="concat(format-number(xs:integer(tokenize(xs:string(timezone-from-date(current-date())), 'T|S|H')[2]), '00'), ':00')"/>
        </xsl:variable>
        <xsl:value-of select="$timezone"/>
    </xsl:function>
    
    <!--
        Returns relative timestamp
    
        Valid formats for the 'date-time' parameter:
        'yyyy-mm-dd hh:mm'
        'yyyy-mm-dd+hh:mm'
        'yyyy-mm-dd hh:mm:ss'
        'yyyy-mm-dd+hh:mm:ss'
    -->
    <xsl:function name="util:relative-timestamp" as="xs:string*">
        <xsl:param name="date-time" as="xs:string"/>
        <xsl:param name="language" as="xs:string?"/>
        <xsl:variable name="date-time-complete">
            <xsl:value-of select="replace($date-time, '\s+|\+', 'T')"/>
            <xsl:if test="count(tokenize(tokenize($date-time, ' ')[2], ':')) &lt; 3">:00</xsl:if>
            <xsl:value-of select="util:get-timezone()"/>
        </xsl:variable>
        <xsl:variable name="date-time-final" select="xs:dateTime($date-time-complete)"/>
        <xsl:variable name="difference" select="current-dateTime() - $date-time-final"/>
        <xsl:choose>
            <xsl:when test="$date-time-final &lt;= current-dateTime() - xs:dayTimeDuration('P7D')">
                <xsl:value-of select="util:format-date($date-time, $language, (), true())"/>
            </xsl:when>
            <xsl:when test="$date-time-final &lt;= current-dateTime() - xs:dayTimeDuration('PT23H')">
                <xsl:choose>
                    <xsl:when test="xs:date($date-time-final) = current-date() - xs:dayTimeDuration('P1D')">
                        <xsl:value-of select="portal:localize('Yesterday')"/>
                    </xsl:when>
                    <xsl:otherwise>
                        <xsl:value-of select="format-dateTime($date-time-final, '[FNn]', $language, (), ())"/>
                    </xsl:otherwise>
                </xsl:choose>
                <xsl:value-of select="concat(' ', portal:localize('at'), ' ', util:format-time(substring-before(tokenize($date-time-complete, 'T')[2], '+'), $language))"/>
            </xsl:when>
            <xsl:when test="$date-time-final &lt;= current-dateTime() - xs:dayTimeDuration('PT45M')">
                <xsl:choose>
                    <xsl:when test="$date-time-final &lt;= current-dateTime() - xs:dayTimeDuration('PT1H30M')">
                        <xsl:variable name="hours" select="if (minutes-from-duration($difference) lt 30) then hours-from-duration($difference) else hours-from-duration($difference) + 1"/>
                        <xsl:value-of select="portal:localize('hours-ago', ($hours))"/>
                    </xsl:when>
                    <xsl:otherwise>
                        <xsl:value-of select="portal:localize('About-an-hour-ago')"/>
                    </xsl:otherwise>
                </xsl:choose>
            </xsl:when>
            <xsl:otherwise>
                <xsl:choose>
                    <xsl:when test="$date-time-final &lt;= current-dateTime() - xs:dayTimeDuration('PT1M30S')">
                        <xsl:variable name="minutes" select="if (seconds-from-duration($difference) lt 30) then minutes-from-duration($difference) else minutes-from-duration($difference) + 1"/>
                        <xsl:value-of select="portal:localize('minutes-ago', ($minutes))"/>
                    </xsl:when>
                    <xsl:when test="$date-time-final &lt;= current-dateTime() - xs:dayTimeDuration('PT50S')">
                        <xsl:value-of select="portal:localize('About-a-minute-ago')"/>
                    </xsl:when>
                    <xsl:when test="$date-time-final &lt;= current-dateTime() - xs:dayTimeDuration('PT25S')">
                        <xsl:value-of select="portal:localize('Less-than-a-minute-ago')"/>
                    </xsl:when>
                    <xsl:otherwise>
                        <xsl:value-of select="portal:localize('Just-now')"/>
                    </xsl:otherwise>
                </xsl:choose>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:function>

    <!--
        Formats time
        
        Valid formats for the 'time' parameter:
        'hh:mm'
        'hh:mm:ss'
    -->
    <xsl:function name="util:format-time" as="xs:string">
        <xsl:param name="time" as="xs:string"/>
        <xsl:param name="language" as="xs:string?"/>
        <xsl:variable name="time-final">
            <xsl:value-of select="$time"/>
            <xsl:if test="count(tokenize($time, ':')) &lt; 3">:00</xsl:if>
            <xsl:value-of select="util:get-timezone()"/>
        </xsl:variable>
        <xsl:variable name="format-string">
            <xsl:choose>
                <!-- Norwegian -->
                <xsl:when test="$language = 'no'">[H01].[m01]</xsl:when>
                <!-- English is default -->
                <xsl:otherwise>[H01]:[m01]</xsl:otherwise>
            </xsl:choose>
        </xsl:variable>
        <xsl:choose>
            <!-- Supplied time is erroneous format -->
            <xsl:when test="not($time-final castable as xs:time)">Erroneous time format</xsl:when>
            <xsl:otherwise>
                <xsl:value-of select="format-time(xs:time($time-final), $format-string, $language, (), ())"/>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:function>

    <!--
        Formats date-time
        
        Valid formats for the 'date-time' parameter:
            'yyyy-mm-dd hh:mm'
            'yyyy-mm-dd+hh:mm'
            'yyyy-mm-dd'
        Valid values for the 'format' parameter:
            'long' (default)
            'short'
        Valid values for the 'include-time' parameter:
            true()
            false() (default)
    -->
    <xsl:function name="util:format-date" as="xs:string">
        <xsl:param name="date-time" as="xs:string"/>
        <xsl:param name="language" as="xs:string?"/>
        <xsl:param name="format" as="xs:string?"/>
        <xsl:param name="include-time" as="xs:boolean?"/>
        <xsl:variable name="format-string">
            <xsl:choose>
                <!-- Norwegian -->
                <xsl:when test="$language = 'no'">
                    <xsl:choose>
                        <!-- Short date format -->
                        <xsl:when test="$format = 'short'">[D01].[M01].[Y0001]</xsl:when>
                        <!-- Long date format -->
                        <xsl:otherwise>[D1]. [MNn] [Y0001]</xsl:otherwise>
                    </xsl:choose>
                </xsl:when>
                <!-- English is default -->
                <xsl:otherwise>
                    <xsl:choose>
                        <!-- Short date format -->
                        <xsl:when test="$format = 'short'">[D01]/[M01]/[Y0001]</xsl:when>
                        <!-- Long date format -->
                        <xsl:otherwise>[D1] [MNn] [Y0001]</xsl:otherwise>
                    </xsl:choose>
                </xsl:otherwise>
            </xsl:choose>
        </xsl:variable>
        <xsl:variable name="final-date-time">
           <xsl:choose>
               <!-- Supplied date is erroneous format -->
               <xsl:when test="not(tokenize($date-time, '[\s+|\+]+')[1] castable as xs:date)">Erroneous date format</xsl:when>
               <xsl:otherwise>
                   <xsl:value-of select="format-date(xs:date(tokenize($date-time, '[\s+|\+]+')[1]), $format-string, $language, (), ())"/>
                   <!-- Time included -->
                   <xsl:if test="$include-time and tokenize($date-time, '[\s+|\+]+')[2]">
                       <xsl:value-of select="concat(' ', util:format-time(tokenize($date-time, '[\s+|\+]+')[2], $language))"/>
                   </xsl:if>
               </xsl:otherwise>
           </xsl:choose>
        </xsl:variable>
        <xsl:value-of select="$final-date-time"/>
    </xsl:function>

    <!-- Displays menu item name -->
    <xsl:function name="util:menuitem-name" as="xs:string">
        <xsl:param name="menuitem" as="item()?"/>
        <xsl:value-of select="if ($menuitem/menu-name != '') then $menuitem/menu-name else $menuitem/display-name != '')"/>
    </xsl:function>

    <!-- Formats bytes -->
    <xsl:function name="util:format-bytes" as="xs:string">
        <xsl:param name="bytes" as="xs:integer"/>
        <xsl:value-of select="if ($bytes > 1073741824) then concat(format-number($bytes div 1073741824, '0.#'), ' GB') else if ($bytes > 1048576) then concat(format-number($bytes div 1048576, '0.#'), ' MB') else if ($bytes > 1024) then concat(format-number($bytes div 1024, '0'), ' KB') else concat($bytes, ' B')"/>
    </xsl:function>

    <!-- Displays icon image -->
    <xsl:template name="utilities.icon-image">
        <xsl:param name="file-name" as="xs:string"/>
        <xsl:param name="icon-folder-path" as="xs:string"/>
        <xsl:param name="icon-image-prefix" select="'icon-'" as="xs:string"/>
        <xsl:param name="icon-image-file-extension" select="'png'" as="xs:string"/>
        <xsl:param name="icon-class" select="'icon text'" as="xs:string?"/>
        <xsl:variable name="file-extension" select="lower-case(tokenize($file-name, '\.')[last()])"/>
        <xsl:variable name="image-url">
            <xsl:value-of select="$icon-folder-path"/>
            <xsl:if test="not(ends-with($icon-folder-path, '/'))">/</xsl:if>
            <xsl:value-of select="$icon-image-prefix"/>
            <xsl:choose>
                <xsl:when test="contains('htm', $file-extension)">
                    <xsl:text>htm</xsl:text>
                </xsl:when>
                <xsl:when test="contains('ppt|pps', $file-extension)">
                    <xsl:text>ppt</xsl:text>
                </xsl:when>
                <xsl:when test="contains('gif|jpg|tif|psd', $file-extension)">
                    <xsl:text>img</xsl:text>
                </xsl:when>
                <xsl:when test="contains('doc|dot', $file-extension)">
                    <xsl:text>doc</xsl:text>
                </xsl:when>
                <xsl:when test="contains('pdf', $file-extension)">
                    <xsl:text>pdf</xsl:text>
                </xsl:when>
                <xsl:when test="contains('avi|mpg|wmv', $file-extension)">
                    <xsl:text>vid</xsl:text>
                </xsl:when>
                <xsl:when test="contains('xls|xlt|csv', $file-extension)">
                    <xsl:text>xls</xsl:text>
                </xsl:when>
                <xsl:when test="contains('xml', $file-extension)">
                    <xsl:text>xml</xsl:text>
                </xsl:when>
                <xsl:when test="contains('txt|dat|text', $file-extension)">
                    <xsl:text>txt</xsl:text>
                </xsl:when>
                <xsl:when test="contains('zip|tar|gz|qz|arj', $file-extension)">
                    <xsl:text>zip</xsl:text>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:text>file</xsl:text>
                </xsl:otherwise>
            </xsl:choose>
            <xsl:if test="not(starts-with($icon-image-file-extension, '.'))">.</xsl:if>
            <xsl:value-of select="$icon-image-file-extension"/>
        </xsl:variable>
        <img src="{portal:createResourceUrl($image-url)}" alt="{concat(util:file-type($file-name), ' ', portal:localize('icon'))}">
            <xsl:if test="$icon-class != ''">
                <xsl:attribute name="class">
                    <xsl:value-of select="$icon-class"/>
                </xsl:attribute>
            </xsl:if>
        </img>
    </xsl:template>

    <!-- Displays file type -->
    <xsl:function name="util:file-type" as="xs:string">
        <xsl:param name="file-name" as="xs:string"/>
        <xsl:variable name="file-extension" select="lower-case(tokenize($file-name, '\.')[last()])"/>
        <xsl:choose>
            <xsl:when test="contains('htm', $file-extension)">
                <xsl:value-of select="portal:localize('HTML')"/>
            </xsl:when>
            <xsl:when test="contains('ppt|pps', $file-extension)">
                <xsl:value-of select="portal:localize('Powerpoint')"/>
            </xsl:when>
            <xsl:when test="contains('gif|jpg|tif|psd', $file-extension)">
                <xsl:value-of select="portal:localize('Image')"/>
            </xsl:when>
            <xsl:when test="contains('doc|dot', $file-extension)">
                <xsl:value-of select="portal:localize('Document')"/>
            </xsl:when>
            <xsl:when test="contains('pdf', $file-extension)">
                <xsl:value-of select="portal:localize('PDF')"/>
            </xsl:when>
            <xsl:when test="contains('avi|mpg|wmv', $file-extension)">
                <xsl:value-of select="portal:localize('Video')"/>
            </xsl:when>
            <xsl:when test="contains('xls|xlt|csv', $file-extension)">
                <xsl:value-of select="portal:localize('Excel')"/>
            </xsl:when>
            <xsl:when test="contains('xml', $file-extension)">
                <xsl:value-of select="portal:localize('XML')"/>
            </xsl:when>
            <xsl:when test="contains('txt|dat|text', $file-extension)">
                <xsl:value-of select="portal:localize('Text')"/>
            </xsl:when>
            <xsl:when test="contains('zip|tar|gz|qz|arj', $file-extension)">
                <xsl:value-of select="portal:localize('ZIP')"/>
            </xsl:when>
            <xsl:otherwise>
                <xsl:value-of select="portal:localize('File')"/>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:function>

    <!-- Crops text -->
    <xsl:function name="util:crop-text" as="xs:string">
        <xsl:param name="source-text" as="xs:string"/>
        <xsl:param name="num-characters" as="xs:integer"/>
        <xsl:choose>
            <xsl:when test="string-length($source-text) gt $num-characters">
                <xsl:value-of disable-output-escaping="yes" select="concat(string-join(tokenize(substring($source-text, 1, $num-characters - 3), '\s')[position() != last()], ' '), '...')"/>
            </xsl:when>
            <xsl:otherwise>
                <xsl:value-of disable-output-escaping="yes" select="$source-text"/>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:function>
    
    <!-- Capitalizes the first character of a string -->
    <xsl:function name="util:capitalize" as="xs:string"> 
        <xsl:param name="string" as="xs:string"/> 
        <xsl:value-of select="concat(upper-case(substring($string, 1, 1)), substring($string, 2))"/>
    </xsl:function>

</xsl:stylesheet>
